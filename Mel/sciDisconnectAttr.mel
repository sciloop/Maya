//
// creator : sciloop
//
// Disconnect the attribute and, if its a multi, delete the multi index.
// Select all relevant nodes.
//
// sciDisconnectAttr( "worldMesh", {} );
// sciDisconnectAttr( "worldMesh", ls( "-l", "-selection ) );
//
//_______________________________________________________________
//
//
global proc sciDisconnectAttr(	string $attr,
								string $sel[] )
{
	string $sn = " // sciloop message : mel : 'sciDisconnectAttr' ";

	if( size($sel)==0 )
	{$sel = ls( "-l", "-selection" );};

	int $disconnectCounter=0;

	for( $node in $sel )
	{
		string $buffer0[] = {};
		int $numTokens0 = tokenize( $attr, ".", $buffer0 );
		string $buffer1[] = {};
		int $numTokens1 = tokenize( $buffer0[$numTokens0-1], "[", $buffer1 );
		string $pureAttr = $buffer1[0];

		int $attrExists = attributeQuery( "-node", $node, "-exists", $pureAttr );

		if( !$attrExists )
		{
			print( $sn+" the attribute : '"+$pureAttr+
				"' does not exist on '"+$node+"'\n" );

			continue;
		}

		if( connectionInfo( "-isDestination", $node+"."+$attr )==true )
		{

print( $sn+" attribute : "+$attr+" is destination\n" );

			string $source[] = listConnections(	"-source",		true,
												"-destination",	false,
												"-plugs",		true,
												$node+"."+$attr );

			catchQuiet( disconnectAttr( $source[0], $node+"."+$attr ) );

			$disconnectCounter++;
		}
		else if( connectionInfo( "-isSource", $node+"."+$attr )==true )
		{

print( $sn+" attribute : "+$attr+" is source\n" );

			string $destinations[] = listConnections(	"-source",		false,
														"-destination",	true,
														"-plugs",		true,
														$node+"."+$attr );
			for( $d in $destinations )
			{
				catchQuiet( disconnectAttr( $node+"."+$attr, $d ) );
				$disconnectCounter++;
			}
		}
		else
		{
			int $list[] = getAttr( "-multiIndices", $node+"."+$attr );

			int $sizeList = size( $list );

			if( !$sizeList )
			{continue;}

print( $list );

			for( $i=0; $i<$sizeList; $i++ )
			{
				if( connectionInfo( "-isDestination", $node+"."+$attr+"["+$i+"]" )==true )
				{
					print( $sn+" attribute : "+$attr+" is destination\n" );

					string $source[] = listConnections(	"-source",		true,
														"-destination",	false,
														"-plugs",		true,
														$node+"."+$attr+"["+$i+"]" );

					catchQuiet( disconnectAttr( $source[0], $node+"."+$attr+"["+$i+"]" ) );
					$disconnectCounter++;
				}
				else if( connectionInfo( "-isSource", $node+"."+$attr+"["+$i+"]" )==true )
				{

//print( $sn+" attribute : "+$attr+" is source.\n" );

					string $destinations[] = listConnections(	"-source",		false,
																"-destination",	true,
																"-plugs",		true,
																$node+"."+$attr+"["+$i+"]" );
					for( $d in $destinations )
					{
						catchQuiet( disconnectAttr( $node+"."+$attr+"["+$i+"]", $d ) );
						$disconnectCounter++;
					}
				}

				catchQuiet( removeMultiInstance( "-break", true, $node+"."+$attr+"["+$i+"]" ) );
			}
		}
	}

	if( $disconnectCounter )
	{print( $sn+" done! Disconnected "+$disconnectCounter+" connection(s).\n" );}
	else
	{print( $sn+" nothing to disconnect!\n" );}
}

//_______________________________________________________________
//
//
